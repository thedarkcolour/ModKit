plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'maven-publish'
}

version = '1.0'
group = 'thedarkcolour.modkit'
archivesBaseName = 'modkit'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.19.4'

    runs {
        client {
            workingDirectory project.file('run')
			
            property 'forge.logging.console.level', 'debug'

            mods {
                modkit {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'

            mods {
                modkit {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
        }

        modkitData {
            workingDirectory project.file('run')

            parent runs.data

            property 'forge.logging.console.level', 'debug'
            args '--mod', 'modkit', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                modkit {
                    source sourceSets.main
                }
            }
        }

        testmodClient {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'

            parent runs.client
            ideaModule "${project.name}.test"

            mods {
                modkit {
                    source sourceSets.main
                }
                testmod {
                    source sourceSets.test
                }
            }
        }

        testmodData {
            workingDirectory project.file('run')

            parent runs.data
            ideaModule "${project.name}.test"

            property 'forge.logging.console.level', 'debug'
            args '--mod', 'testmod', '--all', '--output', file('src/generated_test/resources/'), '--existing', file('src/test/resources')

            mods {
                modkit {
                    source sourceSets.main
                }
                testmod {
                    source sourceSets.test
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }
sourceSets.test.resources { srcDir 'src/generated_test/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.4-45.0.59'
}

processResources {
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
        }
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "modkit",
                "Specification-Vendor"    : "thedarkcolour",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "examplemodsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
